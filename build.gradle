plugins {
  id 'groovy'

  // Apply the application plugin to add support for building a CLI application in Java.
  id 'application'
  id 'jacoco'  // Use `gradlew jacocoTestReport` to create test coverage report.
  id 'codenarc'
}

distTar.enabled = false

codenarcMain {
  configFile = file("$rootDir/config/codenarc/codenarc.groovy")
  ignoreFailures = true
}

codenarcTest {
  configFile = file("$rootDir/config/codenarc/codenarc.groovy")
  ignoreFailures = true
}

distributions {
  main {
    contents {
      from 'src/main/resources'
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.apache.groovy:groovy-all:5.0.3'
  // This dependency is used by the application.
  implementation 'com.google.guava:guava:33.5.0-jre'
  implementation 'net.jthink:jaudiotagger:3.0.1'
  implementation 'org.apache.logging.log4j:log4j-api:2.25.3'
  implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
  implementation 'org.apache.logging.log4j:log4j-jcl:2.25.3'

  testImplementation 'junit:junit:4.13.2'
  testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  codenarc('org.codenarc:CodeNarc:3.7.0-groovy-4.0')
  codenarc('org.apache.groovy:groovy-all:4.0.29') // Required for Java 17
}

// Apply a specific Java toolchain to ease working on different environments.
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

project.version = 'v2.1.3'

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

application {
  mainClass = 'net.ebdon.yttozara.YtToZara'
}

tasks.named('test') {
  useJUnitPlatform()
}
